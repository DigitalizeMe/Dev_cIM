PREFIX ocp: <http://www.semanticweb.org/DigitalizeMe/ontologies/2025/3/OCP#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?this ?other ?earliestStartTime ?latestEndTime ?startInstant ?endInstant
WHERE {
    BIND (<http://www.example.de/example#P3> AS ?this)
    BIND (<http://www.example.de/example#P2> AS ?other)
    ?this time:after ?other .
    {
        ?this time:hasBeginning ?startInstant .
        { ?startInstant ocp:hasActualTime ?startTimeValue . }
        UNION
        { ?startInstant ocp:hasEstimatedTime ?startTimeValue . }
    }
    UNION
    {
        ?this ocp:beginsWith ?startEntity .
        ?startEntity (ocp:beginsWith)* ?start .
        ?start a time:Instant .
        BIND (?start AS ?startInstant)
        { ?start ocp:hasActualTime ?startTimeValue . }
        UNION
        { ?start ocp:hasEstimatedTime ?startTimeValue . }
    }
    UNION
    {
        ?this ocp:hasActualBeginning ?startInstant .
        ?startInstant a time:Instant .
        { ?startInstant ocp:hasActualTime ?startTimeValue . }
        UNION
        { ?startInstant ocp:hasEstimatedTime ?startTimeValue . }
    }
    {
        ?other time:hasEnd ?endInstant .
        { ?endInstant ocp:hasActualTime ?endTimeValue . }
        UNION
        { ?endInstant ocp:hasEstimatedTime ?endTimeValue . }
    }
    UNION
    {
        ?other ocp:endsWith ?endEntity .
        ?endEntity (ocp:endsWith)* ?end .
        ?end a time:Instant .
        BIND (?end AS ?endInstant)
        { ?end ocp:hasActualTime ?endTimeValue . }
        UNION
        { ?end ocp:hasEstimatedTime ?endTimeValue . }
    }
    UNION
    {
        ?other ocp:hasEstimatedEnd ?endInstant .
        ?endInstant a time:Instant .
        { ?endInstant ocp:hasActualTime ?endTimeValue . }
        UNION
        { ?endInstant ocp:hasEstimatedTime ?endTimeValue . }
    }
    BIND (STRDT(STR(?startTimeValue), xsd:date) AS ?earliestStartTime)
    BIND (STRDT(STR(?endTimeValue), xsd:date) AS ?latestEndTime)
    FILTER (?earliestStartTime <= ?latestEndTime)
}